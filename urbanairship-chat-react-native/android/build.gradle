import groovy.json.JsonSlurper

buildscript {
    ext {
        airshipVersion = "16.7.1"

        kotlinVersion = '1.5.31'
        androidToolsVersion = '4.1.3'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${project.ext.androidToolsVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.kotlinVersion}"
    }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

android {

    // Android SDK Versions
    compileSdkVersion 31

    defaultConfig {
        targetSdkVersion 31
        minSdkVersion 21

        buildConfigField("String", "MODULE_VERSION", "\"${getModuleVersion()}\"")
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getModuleVersion() {
    def jsonFile = file('../package.json')
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)

    return parsedJson["version"]
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation("com.urbanairship.android:urbanairship-chat:${project.ext.airshipVersion}") {
        exclude group:'androidx.appcompat', module: 'appcompat'
    }
    api project(':urbanairship-react-native')
}
