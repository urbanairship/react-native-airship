import groovy.json.JsonSlurper

buildscript {
    ext {
        airshipVersion = "16.7.5"

        kotlinVersion = '1.5.31'
        androidToolsVersion = '4.1.3'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:${project.ext.androidToolsVersion}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.kotlinVersion}"
    }
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

android {

    // Android SDK Versions
    compileSdkVersion 31

    defaultConfig {
        targetSdkVersion 31
        minSdkVersion 21

        buildConfigField("String", "MODULE_VERSION", "\"${getModuleVersion()}\"")
        consumerProguardFiles 'proguard-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def getModuleVersion() {
    def jsonFile = file('../package.json')
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)

    return parsedJson["version"]
}

def safeExtGet (prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.appcompat:appcompat:1.3.1"
    
    api ("com.urbanairship.android:urbanairship-fcm:${project.ext.airshipVersion}") {
        exclude group: 'com.google.firebase', module: 'firebase-messaging'
        exclude group:'androidx.appcompat', module: 'appcompat'
    }

    api ("com.urbanairship.android:urbanairship-message-center:${project.ext.airshipVersion}") {
        exclude group:'androidx.appcompat', module: 'appcompat'
    }
    api ("com.urbanairship.android:urbanairship-automation:${project.ext.airshipVersion}") {
        exclude group:'androidx.appcompat', module: 'appcompat'
    }

    implementation "com.google.firebase:firebase-messaging:${safeExtGet('firebaseMessagingVersion', '22.0.0')}"
}
