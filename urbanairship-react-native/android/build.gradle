apply plugin: 'com.android.library'

import groovy.json.JsonSlurper

ext {
    airshipVersion = "14.6.0"
}

android {

    // Android SDK Versions
    compileSdkVersion 29

    defaultConfig {
        targetSdkVersion 29
        minSdkVersion 21

        buildConfigField("String", "MODULE_VERSION", "\"${getModuleVersion()}\"")
        consumerProguardFiles 'proguard-rules.pro'
    }
}

def getModuleVersion() {
    def jsonFile = file('../package.json')
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)

    return parsedJson["version"]
}

def safeExtGet (prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

dependencies {
    implementation 'com.facebook.react:react-native:+'

    api ("com.urbanairship.android:urbanairship-fcm:$airshipVersion") {
      exclude group: 'com.google.firebase', module: 'firebase-messaging'
    }

    api "com.urbanairship.android:urbanairship-message-center:$airshipVersion"
    api "com.urbanairship.android:urbanairship-automation:$airshipVersion"
    api "com.urbanairship.android:urbanairship-location:$airshipVersion"

    implementation "com.google.firebase:firebase-messaging:${safeExtGet('firebaseMessagingVersion', '22.0.0')}"
}
